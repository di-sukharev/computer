class Hunter {

    field Avatar avatar;
    field Moover moover;
    field Labirinth labirinth;

    field int speed, width, height;

    field int direction;
    field boolean isMoving;

    field Cell currentCell, nextCell;

    constructor Hunter new(Labirinth usedLabirinth) {
        var int centerOfCellX, centerOfCellY;
        var Cell startCell;

        let labirinth = usedLabirinth;

        let startCell = labirinth.getCellFromRowCol(0, 0);

        let speed = 2;
        
        let width = 10;
        let height = 10;

        let centerOfCellX = startCell.getX() + (startCell.getWidth() / 2) - (width / 2);
        let centerOfCellY = startCell.getY() + (startCell.getHeight() / 2) - (height / 2);
        let avatar = Avatar.new(centerOfCellX, centerOfCellY, speed, width, height);
        let moover = Moover.new(labirinth, avatar);

        return this;
    }

    method void render() {
        do avatar.render();

        return;
    }

    method void move() {
        if (~isMoving) { do _setHunterDirection(); let isMoving = true; }

        do moover.move(direction);

        if (_hasHunterMoved()) { let isMoving = false; }

        return;
    }

    method void _setHunterDirection() {
        var boolean isWallInThatDirection;

        let isWallInThatDirection = true;

        let currentCell = labirinth.getCurrentCell(avatar);

        while(isWallInThatDirection) {
            let direction = LCGRandom.randRange(1,4);
            let nextCell = labirinth.getCellFromDirection(avatar, direction);
            
            if (~(nextCell = null)) {
                let isWallInThatDirection = (
                    (currentCell.getBorder() = direction)
                    | (nextCell.getBorder() = labirinth.getParallelWall(direction))
                    );
            }
        }

        return;
    }

    method boolean _hasHunterMoved() {
        var boolean centerOfAvatarIsInCenterOfTheNextCell;

        let currentCell = labirinth.getCurrentCell(avatar);

        let centerOfAvatarIsInCenterOfTheNextCell =
            (currentCell = nextCell)
            & ((avatar.getX() + (avatar.getWidth() / 2)) = (nextCell.getX() + (nextCell.getWidth() / 2)))
            & ((avatar.getY() + (avatar.getHeight() / 2)) = (nextCell.getY() + (nextCell.getHeight() / 2)));

        return centerOfAvatarIsInCenterOfTheNextCell;
    }

    method void destroy() {
        do avatar.destroy();
        
        do Memory.deAlloc(this);
        
        return;
    }

    method int getSpeed() {
        return speed;
    }
    
    method int getWidth() {
        return width;
    }
    
    method int getHeight() {
        return height;
    }
}