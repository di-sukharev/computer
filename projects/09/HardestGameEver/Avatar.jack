class Avatar {

    field int x, y; // x,y coordinates on the screen
    field int size; // the width/height of the avatar
    field int speed;

    constructor Avatar new(int initX, int initY, int initSpeed) {
        
        let x = initX;
        let y = initY;
        let size = 5;
        let speed = initSpeed;

        return this;
    }


    method void render() {
        do Screen.setColor(true); // set color to black
        do Screen.drawRectangle(x, y, x + size, y + size);

        return;
    }

    method void moveUp() {
        if (y > 1) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
            let y = y - speed;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + size, y + 1);
        }
        return;
    }
    
    method void moveRight() {
        if ((x + size) < 510) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + 1, y + size);
            let x = x + speed;
            do Screen.setColor(true);
            do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
        }
        return;
    }
    
    method void moveDown() {
        if ((y + size) < 254) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + size, y + 1);
            let y = y + speed;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
        }
        return;
    }
    
    method void moveLeft() {
        if (x > 1) {
            do Screen.setColor(false);
            do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
            let x = x - speed;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 1, y + size);
        }
        return;
    }

    method void destroy() {
        do Memory.deAlloc(this);
        return;
    }

    method int getX() {
        return x;
    }
    
    method int getY() {
        return y;
    }
 
    method int getSpeed() {
        return speed;
    }

}