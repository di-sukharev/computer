class HardestGameEver {

    field Avatar avatar;
    field Labirinth labirinth;
    field Direction direction;

    constructor HardestGameEver new() {
        
        let labirinth = Labirinth.new();
        let avatar = Avatar.new(0,0);
        let direction = 0;

        return this;

    }

    method void run() {
        
        var boolean doesUserWantToExit;
        let doesUserWantToExit = false;

        do initialize();

        while (~doesUserWantToExit) {
            do play();

            let doesUserWantToExit = checkIfUserWantsToExit();
        }

        do destroy();

        return;
    }

    method void initialize() {
        do avatar.render();
        do labirinth.render();
        
        return;
    }

    method void destroy() {
        do labirinth.destroy();
        do avatar.destroy();
        
        do Memory.deAlloc(this);
        
        return;
    }

    method boolean checkIfUserWantsToExit() {
        var char keyPressed;
        var boolean exit;

        let keyPressed = Keyboard.keyPressed();
        let exit = false;
      
        if (keyPressed = 81)  { let exit = true; } // q key

        return exit;
    }

    method void play() {
        do moveAvatar();
        return;
    }

    method void moveAvatar() {

        do defineMoveDirection();

        if (direction = 1) { do avatar.moveLeft(); }
        if (direction = 2) { do avatar.moveUp(); }
        if (direction = 3) { do avatar.moveRight(); }
        if (direction = 4) { do avatar.moveDown(); }
        
        do Sys.wait(5);  // delays the next movement
        
        return;
    }

    method void defineMoveDirection() {
        
        var char keyPressed;        
        let keyPressed = Keyboard.keyPressed();

        if (keyPressed = 0)   { let direction = 0; }   // no key is pressed
        if (keyPressed = 130) { let direction = 1; }   // left arrow
        if (keyPressed = 131) { let direction = 2; }   // up arrow
        if (keyPressed = 132) { let direction = 3; }   // right arrow
        if (keyPressed = 133) { let direction = 4; }   // down arrow

        return;
    }

}